# Makes BoP features generate in default worlds
Tweaker.removeExcludedBOPWorldType("default")

everyBiome = forAllBiomes()
everyBiome.removeDecoration("LAKE_LAVA")
everyBiome.removeDecoration("LAKE_WATER")


everyBiome.removeFeature("LAKE")
everyBiome.removeGeneratorBOP("hot_springs")

# Prevents the player from spawning in "non-friendly biomes"
forBiomes("biomesoplenty:bog", "biomesoplenty:cold_desert", "biomesoplenty:crag", "biomesoplenty:dead_forest", "biomesoplenty:dead_swamp", "biomesoplenty:fen", "biomesoplenty:lavender_fields", "biomesoplenty:mangrove", "biomesoplenty:marsh", "biomesoplenty:ominous_woods", "biomesoplenty:quagmire", "biomesoplenty:steppe", "biomesoplenty:tundra", "biomesoplenty:wasteland", "biomesoplenty:xeric_shrubland", "biomesoplenty:glacier", "biomesoplenty:snowy_tundra", "minecraft:ice_flats", "minecraft:mutated_ice_flats", "minecraft:desert", "minecraft:swampland", "minecraft:ice_mountains", "minecraft:desert_hills", "minecraft:roofed_forest", "minecraft:mesa", "minecraft:mesa_rock", "minecraft:mesa_clear_rock", "minecraft:mutated_desert", "minecraft:mutated_swampland", "minecraft:mutated_roofed_forest", "minecraft:mutated_mesa", "minecraft:mutated_mesa_rock", "minecraft:mutated_mesa_clear_rock", "defiledlands:desert_defiled", "defiledlands:plains_defiled", "defiledlands:forest_tenebra", "defiledlands:forest_vilespine", "defiledlands:hills_defiled", "defiledlands:swamp_defiled", "defiledlands:ice_plains_defiled", "iceandfire:glacier", "minecraft:ocean", "minecraft:frozen_ocean", "minecraft:deep_ocean", "biomesoplenty:coral_reef", "biomesoplenty:kelp_forest").set("isSpawnBiome", false)

# Biome variables
glacier = forBiomes("biomesoplenty:glacier")
vanilla_forest = forBiomes("minecraft:forest")
vanilla_birch_forest = forBiomes("minecraft:birch_forest")
vanilla_taiga = forBiomes("minecraft:taiga")
cold_mountains = forBiomes("minecraft:ice_mountains")
no_water_biomes = forBiomes("biomesoplenty:xeric_shrubland")
prairie = forBiomes("biomesoplenty:prairie")
golden_prairie = forBiomes("biomesoplenty:pasture")
lush_desert = forBiomes("biomesoplenty:lush_desert")
brushland = forBiomes("biomesoplenty:brushland")
quagmire = forBiomes("biomesoplenty:quagmire")
bog = forBiomes("biomesoplenty:bog")
higland = forBiomes("biomesoplenty:highland")
temperate_rainforest = forBiomes("biomesoplenty:temperate_rainforest")
rainforest = forBiomes("biomesoplenty:rainforest")
alps_foothills = forBiomes("biomesoplenty:alps_foothills")
alps = forBiomes("biomesoplenty:alps")
coral_reef = forBiomes("biomesoplenty:coral_reef")
flower_field = forBiomes("biomesoplenty:flower_field")
vanilla_river = forBiomes("minecraft:river")
cherry_blossom_grove = forBiomes("biomesoplenty:cherry_blossom_grove")
chaparral = forBiomes("biomesoplenty:chaparral")
eucalyptus_forest = forBiomes("biomesoplenty:eucalyptus_forest")
mangrove = forBiomes("biomesoplenty:mangrove")
overgrown_cliffs = forbiomes("biomesoplenty:overgrown_cliffs")
steppe = forbiomes("biomesoplenty:steppe")

# Changes glacier top block
Tweaker.setPlacementStage("PRE_POPULATE")
glacier.registerGenBlockRep("minecraft:ice", "minecraft:packed_ice")

Tweaker.setScriptStage("FINISHED_LOAD")
# Mountain tweaks to create better slopes in tandem with custom biome mountains (functional_biomes.cfs)
# Makes vanilla ice mountains as tall as BoP mountains
cold_mountains.set("name", "Cold Mountain Peak")
cold_mountains.set("height", 3.205)
cold_mountains.set("heightVariation", 0.33)
cold_mountains.set("topBlock", "minecraft:snow")
cold_mountains.set("fillerBlock", "minecraft:gravel")
# Renames BoP mountains to be used in warm biomes
warm_mountains = forBiomes("biomesoplenty:mountain")
warm_mountains.set("name", "Warm Mountain Peak")

# Block replacements
Tweaker.setPlacementStage("PRE_POPULATE")
coral_reef.set("topBlock", "minecraft:gravel")

# Raises vanilla forests height so that they don't generate false lakes on their own (height variation only reaches down to sea level when substracted from the new height values)
vanilla_forest.set("height", 0.025)
vanilla_forest.set("heightVariation", 0.125)
vanilla_birch_forest.set("height", 0.08)
vanilla_birch_forest.set("heightVariation", 0.04)
vanilla_taiga.set("height", 0.15)
vanilla_taiga.set("heightVariation", 0.05)
cold_mountains.set("heightVariation", 0.2)
Tweaker.setStage("FINISHED_LOAD")

# Removes water from these biomes completely (remember to add higher-altitude edge biomes around these so that water bodies are not cut)
no_water_biomes.registerGenBlockRep("minecraft:water", "minecraft:air")

# Changes lush desert to have orange sand
lush_desert.set("height", 0.9)
orange_sand = forBlock("undergroundbiomes:igneous_sand")
orange_sand.setProperty("type", "red_granite")
lush_desert.set("topBlock", orange_sand)
orange_sandstone = forBlock("undergroundbiomes:igneous_sandstone")
orange_sandstone.setProperty("type", "red_granite")
lush_desert.set("fillerBlock", orange_sandstone)

# Prairie and golden prairie (pasture) color tweaks
prairie.set("grassColor", 9606732)
prairie.set("foliageColor", 9606732)
golden_prairie.set("grassColor", 9606732)
golden_prairie.set("foliageColor", 9606732)
golden_prairie.set("name", "Golden Prairie")

# Steppe color tweaks
steppe.set("grassColor", 7775040)
steppe.set("foliageColor", 9020999)


# Brushland renamed to Dryland
brushland.set("name", "Dryland")

# Quagmire renamed to Bog and raised to allow for more vegetation/peat
quagmire.set("name", "Bog")
quagmire.set("height", 0.119)
quagmire.set("grassColor", 13944451)
quagmire.set("foliageColor", 13944451)

# Bog renamed to Peatland Edge
bog.set("name", "Peatland Edge")