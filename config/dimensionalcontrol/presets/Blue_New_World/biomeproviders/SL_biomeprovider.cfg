
# Blue's New World script.
# Originally written by BlueFire/BlueFeuer.
# If you are not actively working on the biome map, be sure to disable the biome printer in Dimensions.cfg!


#Generate Humidity Map (4 biomes, random)

#Creates a flat map with 1 biome.
#"minecraft:plains" is equal to a biome ID of 1.
#Conversely, "minecraft:ocean" is equal to a biome ID of 0.
#In other words, these two functions are identical:
#   genLayerHumidity = new GenLayerBaseSingle(1, "minecraft:plains")
#   genLayerHumidity = new GenLayerBaseSingle(1, 1)
#But for this file I'll be using integer IDs until we get to actual biome placement.
#The first parameter in the majority of functions is the seed and can be largely ignored except to increment it to get marginally more random terrain.
genLayerHumidity = new GenLayerBaseSingle(1, "minecraft:plains")

# SECTION 0 - OCEAN SIZE AND HUMIDITY BANDING
#There's a few things you can change here to affect ocean size.
#Replace ID 1 with ID 0 and 1 randomly.
#0s are oceans, 1s are non-oceans.
#The last number is a weight, so if you have 10 and 30 for the last number of each addBiomes respectively you will have 25% (10/40) ocean and 75% (30/40) non-ocean. Don't drop the weight below 10.
genLayerHumidity = new GenLayerHeatToBiomes(1000, genLayerHumidity)
genLayerHumidity.addBiome(1, 0, 10)
genLayerHumidity.addBiome(1, 1, 30)

#Replace oceans surrounded by non-oceans with non-ocean and vice-versa.
#Uncomment this if you want oceans to be able to be larger without removing small oceans.
#Expect more variance and sporadic-ness in size.
#genLayerHumidity = new GenLayerTouching(1001, genLayerHumidity)
#genLayerHumidity.addData(0, 1, null, 6, 1)
#genLayerHumidity.addData(1, 0, null, 6, 0)

#Zoom the layer out, making the oceans twice as big.
#Uncomment this if you want oceans that are twice as large, but also twice as far apart.
genLayerHumidity = new GenLayerZoom(1002, genLayerHumidity)

#Generate humidity bands, then replace any 1s (representing non-oceans) with the bands.
#Uncomment the next six lines if you want banded humidity, and comment out the four lines mentioned below this.
#genLayerHumidityBanded = new GenLayerHeatZ(1, [1, 2, 3, 2])
#genLayerHumidityBanded = new GenLayerZoom(1009, genLayerHumidityBanded)
#genLayerHumidity = new GenLayerCombine(2000, genLayerHumidity, genLayerHumidityBanded)
#genLayerHumidity.addCombination([ 1, 1, 1 ]) #Replace 1 and 1 with 1
#genLayerHumidity.addCombination([ 1, 2, 2 ]) #Replace 1 and 2 with 2
#genLayerHumidity.addCombination([ 1, 3, 3 ]) #Replace 1 and 3 with 3

#Replace 1s (representing non-oceans) with the other humidities. As before, the last number is a weight.
#Comment the next four lines out if you want banded humidity.
genLayerHumidity = new GenLayerHeatToBiomes(1000, genLayerHumidity)
genLayerHumidity.addBiome(1, 1, 10)
genLayerHumidity.addBiome(1, 2, 10)
genLayerHumidity.addBiome(1, 3, 10)

# END SECTION 0
#.. Sort of. There's more you can do below but the important stuff is above this.

#genLayerHumidity.addData(X, Y, Z, W, T)
#Replace X touching Y (but not touching Z) W times with T. Simple!
#X, Y, and Z can be an array and represent a biome.
genLayerHumidity = new GenLayerTouching(1001, genLayerHumidity)
#Attempt to smoothen the map a bit by replacing a biome surrounded by a different biome on 5/8 sides with said different biome.
genLayerHumidity.addData([0, 1, 2], 3, null, 5, 3)
genLayerHumidity.addData([0, 1, 3], 2, null, 5, 2)
genLayerHumidity.addData([0, 3, 2], 1, null, 5, 1)
genLayerHumidity.addData([3, 1, 2], 0, null, 5, 0)

#Repeat, but only for Humid/Dry climates touching
genLayerHumidity = new GenLayerTouching(1002, genLayerHumidity)
#Replace any Dry climates surrounded by a lot of Humid with more Humid and vice-versa.
#I have no problem with Dry and Humid touching, but not surrounding.
genLayerHumidity.addData(1, 3, null, 6, 3)
genLayerHumidity.addData(3, 1, null, 6, 1)


#Generate Heat Map (4 biomes, banded)

#Create Z repeating heat map. Vanilla uses 1, 2, 3 and 4 for heat levels. 1 is hot, 2 is warm, 3 is cool, 4 is freezing.
#genLayerTemperature = new GenLayerHeatZ(1, [2, 1, 2, 3, 4, 3])
#Extended temperatures, just making each climate larger. Intentionally made Hot and Freezing climates 50% thicker, remove a 1 and a 4 to undo that.
genLayerTemperature = new GenLayerHeatZ(1, [2, 2, 1, 1, 1, 2, 2, 3, 3, 4, 4, 4, 3, 3])
#.. And that's it!
#If you want the heat layer to extend further, you can either uncomment this zoom for double the length and extra randomness or simply add more numbers.
#genLayerTemperature = new GenLayerZoom(1005, genLayerTemperature)

#Generate Beach Type Map (3 biomes, random 2/1/1)
#This will be used much much later. It is being generated now so it can be zoomed 1:1 with the climate layer.
genLayerBeach = new GenLayerBaseSingle(1, 1)

#Replace ID 1 with ID 16, 25, 26 randomly.
#These three IDs correspond to the 3 vanilla beach IDs.
#These do not need to be these IDs, they can be any arbitrary ID belonging to a biome that exists that does not get used in the biome map before beaches are actually set.
#Conveniently, the three vanilla beaches meet this criteria.
#If you want to change them, keep in mind they are used further down.
genLayerBeach = new GenLayerHeatToBiomes(1000, genLayerBeach)
genLayerBeach.addBiome(1, 16, 20)
genLayerBeach.addBiome(1, 25, 10)
genLayerBeach.addBiome(1, 26, 10)




#Merge Heat map into Climate map.
#10 = Hot Ocean, 20 = Warm Ocean, 30 = Cool Ocean, 40 = Frozen Ocean.
#11 = Hot/Dry, 12 = Hot/Normal, 13 = Hot/Humid, 21 = Warm/Dry, 22 = Warm/Normal, 23 = Warm/Humid, 31 = Cool/Dry, 32 = Cool/Normal, 33 = Cool/Humid, 41 = Freezing/Dry, 42 = Freezing/Normal, 43 = Freezing/Humid.
#For whatever reason, the second parameter in this function gets it's seed reset.
#In other words, whatever it generates will be identical across seeds and worlds.
#Fortunately, temperature is banded and this quirk does not matter here.
genLayerClimate = new GenLayerCombine(2000, genLayerHumidity, genLayerTemperature)
#Hot
genLayerClimate.addCombination([ 0, 1, 10 ]) #Ocean
genLayerClimate.addCombination([ 1, 1, 11 ]) #Humid
genLayerClimate.addCombination([ 2, 1, 12 ]) #Normal
genLayerClimate.addCombination([ 3, 1, 13 ]) #Dry
#Warm
genLayerClimate.addCombination([ 0, 2, 20 ]) #Ocean
genLayerClimate.addCombination([ 1, 2, 21 ]) #Humid
genLayerClimate.addCombination([ 2, 2, 22 ]) #Normal
genLayerClimate.addCombination([ 3, 2, 23 ]) #Dry
#Cool
genLayerClimate.addCombination([ 0, 3, 30 ]) #Ocean
genLayerClimate.addCombination([ 1, 3, 31 ]) #Humid
genLayerClimate.addCombination([ 2, 3, 32 ]) #Normal
genLayerClimate.addCombination([ 3, 3, 33 ]) #Dry
#Freezing
genLayerClimate.addCombination([ 0, 4, 40 ]) #Ocean
genLayerClimate.addCombination([ 1, 4, 41 ]) #Humid
genLayerClimate.addCombination([ 2, 4, 42 ]) #Normal
genLayerClimate.addCombination([ 3, 4, 43 ]) #Dry


#Zoom Beach and Climate

genLayerClimate = new GenLayerZoom(2011, genLayerClimate)
genLayerBeach = new GenLayerZoom(2012, genLayerBeach)

#Add Swampy climate to Climate Map next to oceans for Warm and Cool climates.
#Swamp/Humid/Normal/Dry Distribution next to Oceans is currently 0/1/1/1.

genLayerClimate = new GenLayerTouching(1001, genLayerClimate)
#Replace 50% of Humid and 25% of Normal climates bordering Ocean with Swampy.
#Distr: 0.75/0.5/0.75/1
genLayerClimate.addData(22, [20, 30], null, 1, 24, 4)
genLayerClimate.addData(23, [20, 30], null, 1, 24, 4)
genLayerClimate.addData(32, [20, 30], null, 1, 34, 2)
genLayerClimate.addData(33, [20, 30], null, 1, 34, 2)

genLayerClimate = new GenLayerTouching(1001, genLayerClimate)
#Replace Dry climates bordering Ocean with Normal 50% of the time.
#Distr: 0.75/0.5/1.25/0.5
genLayerClimate.addData(21, [20, 30], null, 1, 22, 2)
genLayerClimate.addData(31, [20, 30], null, 1, 32, 2)
#Replace Normal climates bordering Ocean with Humid 50% of the time.
#This happens simultaneously with the dry climate replacement, so only climates that are already normal will be replaced.
#Distr: 0.75/0.83/0.83/0.5
genLayerClimate.addData(22, [20, 30], null, 1, 23, 2)
genLayerClimate.addData(32, [20, 30], null, 1, 33, 2)

#Replace swamps touching frozen biomes with humid biomes. Don't want a swampland bordering the north pole.
genLayerClimate = new GenLayerTouching(1001, genLayerClimate)
genLayerClimate.addData(24, [10, 11, 12, 13, 40, 41, 42, 43], null, 1, 23)
genLayerClimate.addData(34, [10, 11, 12, 13, 40, 41, 42, 43], null, 1, 33)

genLayerClimate = new GenLayerZoom(2013, genLayerClimate)
genLayerBeach = new GenLayerZoom(2014, genLayerBeach)
#genLayerClimate = new GenLayerZoom(2015, genLayerClimate)
#genLayerBeach = new GenLayerZoom(2016, genLayerBeach)

#Start River generation
genLayerRivers = new GenLayerRiverInit(100, genLayerClimate)


#Replace Oceans touching other Oceans on all 8 sides with Island 2.5% of the time.
genLayerClimate = new GenLayerTouching(1001, genLayerClimate)
genLayerClimate.addData(10, [10, 20, 30, 40], null, 8, 15, 40)
genLayerClimate.addData(20, [10, 20, 30, 40], null, 8, 25, 40)
genLayerClimate.addData(30, [10, 20, 30, 40], null, 8, 35, 40)
genLayerClimate.addData(40, [10, 20, 30, 40], null, 8, 45, 40)

#Replace Oceans touching other Oceans on all 8 sides with Deep Oceans.
genLayerClimate = new GenLayerTouching(1001, genLayerClimate)
genLayerClimate.addData(10, [10, 20, 30, 40], null, 8, 16)
genLayerClimate.addData(20, [10, 20, 30, 40], null, 8, 26)
genLayerClimate.addData(30, [10, 20, 30, 40], null, 8, 36)
genLayerClimate.addData(40, [10, 20, 30, 40], null, 8, 46)


# SECTION 1 - BIOMES
#Time to assign biomes.
#11 = Hot/Dry, 12 = Hot/Normal, 13 = Hot/Humid, 15 = Hot/Island
#21 = Warm/Dry, 22 = Warm/Normal, 23 = Warm/Humid, 24 = Warm/Swampy, 25 = Warm/Island
#31 = Cool/Dry, 32 = Cool/Normal, 33 = Cool/Humid, 34 = Cool/Swampy, 35 = Cool/Island
#41 = Freezing/Dry, 42 = Freezing/Normal, 43 = Freezing/Humid, 45 = Freezing/Island
#I have only added two biomes to each climate. They are examples with not much consideration behind them.
#Potential considerations:
#I highly recommend adding Biomes O Plenty, Traverse, Project: Vibrant Journeys, and any other biome mods if your choosing to fill out this list.
#Including a biome mod does not mean all of it's biomes need be represented. You can pick and choose the ones you like, and where there's overlap simply pick your favorite.
#Regarding P:VJ specifically; I would recommend turning off most of it's world decor and adding them back in more appropriate locations via another mod such as COFH:World or Cave Generator, unless you don't like the look of them in which case leave them.
#Regarding Biomes O Plenty; You do not have to use Biomes O Plenty world type to use it's biomes, though it's biomes may be missing features such as fallen leaves that may need to be re-added. Or not, if you like it better that way. Either way I would recommend against using BoP's world type.
#ANY biome can be added here, including those from other dimensions such as the Twilight Forest or Atum. You need not limit yourself.
#Humidity is merely a suggestion, not a rule. Swampy just guarantees the biome generates close to oceans, so you could add rocky waters or other non-swamp biomes here.
#Personally, I would add swamplands to warm climates and use the cool swampy area for more unique biomes.
genLayerBiomes = new GenLayerHeatToBiomes(1000, genLayerClimate)
#genLayerBiomes.addBiome(Climate, Biome, Weight)
#Hot/Dry biomes
genLayerBiomes.addBiome(11, "biometweaker:anchor_cove", 10)
#Hot/Normal biomes
genLayerBiomes.addBiome(12, "biometweaker:anchor_cove", 10)
#Hot/Humid biomes
genLayerBiomes.addBiome(13, "biometweaker:anchor_cove", 10)
#Hot/Island biomes
genLayerBiomes.addBiome(15, "biometweaker:anchor_cove", 10)

#Warm/Dry biomes
genLayerBiomes.addBiome(21, "biometweaker:anchor_cove", 10)
#Warm/Normal biomes
genLayerBiomes.addBiome(22, "biometweaker:anchor_cove", 10)
#Warm/Humid biomes
genLayerBiomes.addBiome(23, "biometweaker:anchor_cove", 10)
#Warm/Swampy biomes
genLayerBiomes.addBiome(24, "biometweaker:anchor_cove", 10)
#Warm/Island biomes
genLayerBiomes.addBiome(25, "biometweaker:anchor_cove", 10)

#Cool/Dry biomes
genLayerBiomes.addBiome(31, "biometweaker:blossoming_lands", 10)
#Cool/Normal biomes
genLayerBiomes.addBiome(32, "biometweaker:blossoming_lands", 10)
#Cool/Humid biomes
genLayerBiomes.addBiome(33, "biometweaker:blossoming_lands", 10)
#Cool/Swampy biomes
genLayerBiomes.addBiome(34, "biometweaker:blossoming_lands", 10)
#Cool/Island biomes
genLayerBiomes.addBiome(35, "biometweaker:blossoming_lands", 10)

#Freezing/Dry biomes
genLayerBiomes.addBiome(41, "biometweaker:blossoming_lands", 10)
#Freezing/Normal biomes
genLayerBiomes.addBiome(42, "biometweaker:blossoming_lands", 10)
#Freezing/Humid biomes
genLayerBiomes.addBiome(43, "biometweaker:blossoming_lands", 10)
#Freezing/Island biomes
genLayerBiomes.addBiome(45, "biometweaker:blossoming_lands", 10)

# END SECTION 1

# SECTION 2 - OCEAN BIOMES
#Oceans
#Obviously using only vanilla (1.12) biomes there's not much interesting that can be done here.
#Feel free to fix this egregious oversight.
#You can define as many ocean biomes in each category as you like.
#Whatever biomes are used here must be defined in a few other places that can be found by searching for "OCEANBIOMES".
#10 = Hot Ocean, 20 = Warm Ocean, 30 = Cool Ocean, 40 = Frozen Ocean.
#16 = Hot Deep Ocean, 26 = Warm Deep Ocean, 36 = Cool Deep Ocean, 46 = Frozen Deep Ocean.
# Hot Ocean
genLayerBiomes.addBiome(10, "biometweaker:rocky_skylands", 10)
# Warm Ocean
genLayerBiomes.addBiome(20, "biometweaker:rocky_skylands", 10)
# Cool Ocean
genLayerBiomes.addBiome(30, "biometweaker:rocky_skylands", 10)
# Frozen Ocean
genLayerBiomes.addBiome(40, "biometweaker:rocky_skylands", 10)
# Hot Deep Ocean
genLayerBiomes.addBiome(16, "biometweaker:rocky_skylands", 10)
# Warm Deep Ocean
genLayerBiomes.addBiome(26, "biometweaker:rocky_skylands", 10)
# Cool Deep Ocean
genLayerBiomes.addBiome(36, "biometweaker:rocky_skylands", 10)
# Frozen Deep Ocean
genLayerBiomes.addBiome(46, "biometweaker:rocky_skylands", 10)



# END SECTION 2

genLayerRivers = new GenLayerZoom(2012, genLayerRivers)
#Double zoom for genLayerBiomes to get bigger biomes
genLayerBiomes = new GenLayerZoom(2017, genLayerBiomes)
#commented out, as the biomes were getting too large
#genLayerBiomes = new GenLayerZoom(2017, genLayerBiomes)
genLayerBeach = new GenLayerZoom(2018, genLayerBeach)

# SECTION 3 - LARGE SUB BIOMES
#Roughly 2-8x the size of a small sub biome.
#genLayerBiomes = new GenLayerTouching(1001, genLayerBiomes)
# Examples
#Turns a varying percentage of most biomes into Mountains
#1/4th (25%) of prairies into golden prairies
#genLayerBiomes.addData("biomesoplenty:prairie", "biomesoplenty:prairie", null, 4, "biomesoplenty:pasture", 3)



# END SECTION 3

# SECTION 3.1 - LAKE BIOMES
#Roughly 2-8x the size of a small sub biome.
#Potential considerations:
#Lakes. Probably add a custom biometweaker lake instead of using rivers as I do here.
#genLayerBiomes = new GenLayerTouching(1002, genLayerBiomes)
# Examples
#1/20th of Taigas and Shields into volcanic lakes 
#genLayerBiomes.addData("minecraft:taiga", "minecraft:taiga", null, 4, "biometweaker:volcanic_lake", 20)


# END SECTION 3.1

genLayerRivers = new GenLayerZoom(2012, genLayerRivers)
#Double zoom for genLayerBiomes to get yet bigger biomes and larger sub biomes
genLayerBiomes = new GenLayerZoom(2024, genLayerBiomes)
genLayerBiomes = new GenLayerZoom(2024, genLayerBiomes)
#Beach layer has been kept at the same zoom level with the climate map up 'til now, but I actually want it to be a bit smaller. So this last one is skipped.
#genLayerBeach = new GenLayerZoom(2020, genLayerBeach)

# SECTION 4 - SMALL SUB BIOMES
#Roughly 1/2-1/8th the size of a large sub biome.
#genLayerBiomes = new GenLayerTouching(1003, genLayerBiomes)
# Examples
#1/2 (50%) of Mesas turn into Mesa Plateaus
#genLayerBiomes.addData("minecraft:mesa", "minecraft:mesa", null, 4, "minecraft:mesa_rock", 2)


# END SECTION 4

# SECTION 5 - EDGE BIOMES
#Examples
#Edge biomes. These will replace the outside edge of a biome.
#genLayerBiomes = new GenLayerBiomeEdgeDC(1001, genLayerBiomes)
# I couldn't think of an example so Savannas are now always surrounded by Savanna Plateaus.
#genLayerBiomes.addBiomeEdge("minecraft:savanna", "minecraft:savanna_rock")

# END SECTION 5


# SECTION 5.1 - EDGE EDGE BIOMES
#Edge biomes for edge biomes like mountain foothills. I'm edging an aneurysm here.
#genLayerBiomes = new GenLayerBiomeEdgeDC(1001, genLayerBiomes)
#Edge within edge biomes. These will replace the outside edge of a base biome and function as yet another edge between the original edge and the base biome.
#genLayerBiomes.addBiomeEdge(biome, whitelist, blacklist, replacement)
#genLayerBiomes.addBiomeEdge("biomeX", [ "biomeY" ], null, "biomeZ")
# Examples
# Generates Mountain base around mountains (ice_mountains) and adds mountain foothills between the two.
#genLayerBiomes.addBiomeEdge("biomesoplenty:xeric_shrubland", "biometweaker:drylands_border")
#genLayerBiomes.addBiomeEdge("minecraft:ice_mountains", "biometweaker:mountain_slope")
#genLayerBiomes.addBiomeEdge("biometweaker:atacamian_desert", "biometweaker:drylands_border")

# END SECTION 5.1

#Beach Generation

#Replace Oceans bordering another biome with beaches.
genLayerBiomes = new GenLayerBiomeEdgeDC(1001, genLayerBiomes)
#OCEANBIOMES
genLayerBiomes.addBiomeEdge([ "biometweaker:rocky_skylands" ], 16)

#Finally using that beach layer made ages ago.
#As before, GenLayerCombine seems to be bugged and the second parameter has it's seed reset.
#This means beach types will appear very loosely in the same general areas across worlds, but it should hardly be noticeable, even if you know it's happening.
genLayerBiomes = new GenLayerCombine(2000, genLayerBiomes, genLayerBeach)
genLayerBiomes.addCombination([ 16, 16, 16 ])
genLayerBiomes.addCombination([ 16, 25, 25 ])
genLayerBiomes.addCombination([ 16, 26, 26 ])

# SECTION 6 - BEACHES
#The beaches are now in place and are split 50/25/25 for beach/stonebeach/snowbeach.
#They can now be manually corrected for the various biome types.
#16 = normal beach, 25 = stone beach, 26 = snowy beach.
#Any biome not represented here WILL have normal, stone, and snowy beaches, which is probably undesirable.
#Potential considerations:
#Stony beaches are NOT meant to be beaches to normal biomes. Recommend replacing them with gravel beaches from a mod.
genLayerBiomeBeaches = new GenLayerBiomeEdgeDC(1001, genLayerBiomes)
# Examples
# Biomes with no special beach have their snowy beaches replaced with normal beaches.
#genLayerBiomeBeaches.addBiomeEdge(26, [ "minecraft:savanna", "minecraft:forest", "minecraft:roofed_forest", "minecraft:plains", "minecraft:birch_forest", "biometweaker:shield_volcano", "biomesoplenty:lavender_fields", "biometweaker:mountain_base" ], null, "minecraft:beaches")
# Stony shores get gravel beaches.
#genLayerBiomeBeaches.addBiomeEdge(26, [ "minecraft:redwood_taiga", "minecraft:taiga", "biomesoplenty:redwood_forest", "biomesoplenty:redwood_forest_edge" ], null, "biomesoplenty:gravel_beach")

# END SECTION 6

genLayerBiomeBeaches = new GenLayerZoom(2018, genLayerBiomeBeaches)
genLayerRivers = new GenLayerZoom(2012, genLayerRivers)


genLayerRivers = new GenLayerZoom(2012, genLayerRivers)
genLayer = new GenLayerZoom(2012, genLayerBiomeBeaches)

genLayerRivers = new GenLayerZoom(2012, genLayerRivers)
genLayer = new GenLayerZoom(2012, genLayer)

genLayerRivers = new GenLayerZoom(2050, genLayerRivers)

genLayerRivers = new GenLayerRiver(1505, genLayerRivers)
genLayerRivers = new GenLayerSmooth(1000, genLayerRivers)
genLayerRivers = new GenLayerZoom(2012, genLayerRivers)

# SECTION 7 - RIVERS
#If a biome is not represented below, they will have normal rivers.
#Potential considerations:
#Removing rivers from swamplands and mushroom islands.
#Creating a new river in BiomeTweaker for dried rivers in deserts. Maybe add a variant desert specifically for it. Or add an oasis desert specifically without it. You can add an edge biome to deserts with the river set to itself to separate dried and normal rivers.
#Extra deep river variant for mountains so rivers reach sea level.
finalGenLayer1 = new GenLayerRiverMixDC(100, genLayer, genLayerRivers)
#OCEANBIOMES
# Examples
# Rivers that generate in oceans should just be ocean.
#finalGenLayer1.setRiverBiome("minecraft:ocean", "minecraft:ocean")
finalGenLayer1.setRiverBiome("biometweaker:rocky_skylands", "biometweaker:rocky_skylands")

# END SECTION 7

finalGenLayer2 = new GenLayerVoronoiZoom(10, finalGenLayer1)
setGenLayers(finalGenLayer1, finalGenLayer2)